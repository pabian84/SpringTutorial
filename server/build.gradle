plugins {
    id 'java'
    id 'org.springframework.boot' version '4.0.1' // 2025.12 릴리즈
    id 'io.spring.dependency-management' version '1.1.7' // 2024.12 릴리즈
    id 'eclipse'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
// Spring Boot 4.0은 Java 21 LTS가 베이스라인입니다. (17 -> 21 변경)
java { sourceCompatibility = '21' }

repositories { mavenCentral() }

dependencies {
    // -- Spring Boot Core Starter --
    // Spring Boot 캐시 스타터
    implementation 'org.springframework.boot:spring-boot-starter-cache'
    // Spring Boot 웹 애플리케이션
    implementation 'org.springframework.boot:spring-boot-starter-web'
    // 웹소켓 기능
    implementation 'org.springframework.boot:spring-boot-starter-websocket'
    // Spring Boot 4.0 필수: RestTemplateBuilder 및 RestClient 사용을 위한 스타터
    implementation 'org.springframework.boot:spring-boot-starter-restclient'
    // Spring Security (보안 점검)
    implementation 'org.springframework.boot:spring-boot-starter-security'
    // Spring Boot 테스트 스타터
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    // --- Database & MyBatis ---
    // MyBatis Starter: Spring Boot 4.0 호환 버전 (2025.12.01 출시)
    implementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter:4.0.1'
    // H2 데이터베이스 (테스트용 인메모리 DB)
    implementation 'com.h2database:h2'

    // Swagger (API 문서 자동화)
    // Spring Boot 4.0 호환 버전 (2026.01.01 출시)
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:3.0.1'
    // SpringDoc 오류 해결을 위한 Actuator (서버 상태 모니터링 및 설정 지원)
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    // Caffeine 캐시 라이브러리 추가
    implementation 'com.github.ben-manes.caffeine:caffeine'
    // 하드웨어 정보 조회 Operation Systeim & Hardware Information
    // 최신 안정 버전 (2026.01.08 출시)
    implementation 'com.github.oshi:oshi-core:6.9.2'

    // JWT (출입증 발급기)
    // 최신 버전 (2025.08.20 출시)
    implementation 'io.jsonwebtoken:jjwt-api:0.13.0'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.13.0'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.13.0'

    // lombok (코드 간결화)
    compileOnly 'org.projectlombok:lombok'
    // lombok 애노테이션 프로세서
    annotationProcessor 'org.projectlombok:lombok'

    // yml 설정을 클래스로 매핑하고, IDE에 자동완성 정보를 제공하는 처리기
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
}

// 이 부분이 작동하려면 맨 위 plugins에 id 'eclipse'가 필수입니다.
// VS Code가 컴파일할 때 파라미터 이름을 유지하도록 내부 설정을 덮어씌웁니다.
eclipse {
    jdt {
        file {
            withProperties { props ->
                props['org.eclipse.jdt.core.compiler.codegen.methodParameters'] = 'generate'
            }
        }
    }
}

// 자바 컴파일 시 파라미터 이름을 유지하도록 설정 (Spring Boot 3.2+ 필수)
// 인코딩 설정 추가
tasks.withType(JavaCompile) {
    options.compilerArgs << '-parameters'
    options.encoding = 'UTF-8'  // [추가] 소스 코드 인코딩을 UTF-8로 고정
}

// 실행할 때도 UTF-8로 실행하도록 설정 (bootRun 등 사용 시)
// tasks.withType(JavaExec) {
//     systemProperty "file.encoding", "UTF-8"
// }